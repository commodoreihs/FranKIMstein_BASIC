; ===
; CHANGE: Removed TI$ assignment code - TIMELP/TIMNUM/etc
; TI$ is now read-only (always returns 000000)
GETSPT LDY #2
 LDA (FACMO),y
 CMP FRETOP+1
 BCC DNTCPY
 BNE QVARIA
 DEY
 LDA (FACMO),y
 CMP FRETOP
 BCC DNTCPY
QVARIA LDY FACLO
 CPY VARTAB+1
 BCC DNTCPY
 BNE COPY
 LDA FACMO
 CMP VARTAB
 BCS COPY
DNTCPY LDA FACMO
 LDY FACMO+1
 JMP COPYC
COPY LDY #0
 LDA (FACMO),y
 JSR STRINI
 LDA DSCPNT
 LDY DSCPNT+1
 STA STRNG1
 STY STRNG1+1
 JSR MOVINS
 LDA #<DSCTMP
 LDY #>DSCTMP
COPYC STA DSCPNT
 STY DSCPNT+1
 JSR FRETMS
 LDY #0
 LDA (DSCPNT),y
 STA (FORPNT),y
 INY
 LDA (DSCPNT),y
 STA (FORPNT),y
 INY
 LDA (DSCPNT),y
 STA (FORPNT),y
 RTS
; CHANGE: Simplified PRINT# and CMD - no channel support
; Just skip the channel number and fall through to PRINT
PRINTN
CMD      jsr GETBYT       ; Parse and ignore channel number
         jsr CHRGOT
         beq PRINT        ; If end of line, just do CRDO
         cmp #','
         bne PRINT
         jsr CHRGET       ; Skip comma
         jmp PRINT
STRDON JSR STRPRT
NEWCHR JSR CHRGOT
PRINT BEQ CRDO
PRINTC BEQ PRTRTS
 CMP #TABTK
 BEQ TABER
 CMP #SPCTK
 CLC
 BEQ TABER
 CMP #44
 BEQ COMPRT
 CMP #59
 BEQ NOTABR
 JSR FRMEVL
 BIT VALTYP
 BMI STRDON
 JSR FOUT
 JSR STRLIT
 JSR STRPRT
 JSR OUTSPC
 BNE NEWCHR
FININL LDA #0
 STA BUF,X
ZZ5=BUF-1
 LDX #<ZZ5
 LDY #>ZZ5
 LDA CHANNL
 BNE PRTRTS
CRDO LDA #13
 JSR OUTDO
 ; Fall through to CRFIN
CRFIN LDA #10        ; CHANGE: Always output LF for serial terminal
 JSR OUTDO
 LDA #255           ; Set A to non-zero for flag
PRTRTS RTS
COMPRT SEC
 JSR PLOT        ;GET TAB POSITION IN X
 TYA
NCMPOS =$1E
 SEC
MORCO1 SBC #CLMWID
 BCS MORCO1
 EOR #255
 ADC #1
 BNE ASPAC
TABER PHP
 SEC
 JSR PLOT        ;READ TAB POSITION
 STY TRMPOS
 JSR GTBYTC
 CMP #41
 BNE SNERR4
 PLP
 BCC XSPAC
 TXA
 SBC TRMPOS
 BCC NOTABR
ASPAC TAX
XSPAC INX
XSPAC2 DEX
 BNE XSPAC1
NOTABR JSR CHRGET
 JMP PRINTC

